plugins {
    id "io.gitlab.arturbosch.detekt" version "$detekt_version"
    id "com.android.application"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
    id "kotlin-android"
    id "kotlin-parcelize"
    id "kotlin-kapt"
    id "androidx.navigation.safeargs.kotlin"
    id "dagger.hilt.android.plugin"
    id 'com.google.firebase.firebase-perf'
}

//apply from: 'https://raw.githubusercontent.com/Hipo/macaron/master/config/quality/quality.gradle'

def browsersGradlePath = 'browsers.gradle'
def offrampGradlePath = 'offramp.gradle'

if (project.file(browsersGradlePath).exists()) {
    apply from: browsersGradlePath
}
if (project.file(offrampGradlePath).exists()) {
    apply from: offrampGradlePath
}

android {
    compileSdkVersion targets.compileSdkVersion
    defaultConfig {
        versionCode peraApplication.versionCode
        versionName peraApplication.versionName
        applicationId application.applicationId
        minSdkVersion targets.minSdkVersion
        targetSdkVersion targets.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        releaseLocal {
            def localProps = new Properties()
            try {
                localProps.load(new FileInputStream(file('../local.properties')))
                storeFile file(localProps["RELEASE_STORE_FILE"])
                storePassword localProps["RELEASE_STORE_PASSWORD"]
                keyAlias localProps["RELEASE_KEY_ALIAS"]
                keyPassword localProps["RELEASE_KEY_PASSWORD"]
            } catch (e) {
                println(e)
            }
        }
    }

    buildTypes {
        release {
            // signingConfig signingConfigs.releaseLocal //enable this to sign the release
            multiDexEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [enableCrashReporting: "true", enableFirebasePerformanceLogcat: "false"]
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix " (DEBUG)"
            multiDexEnabled true
            minifyEnabled false
            manifestPlaceholders = [enableCrashReporting: "false", enableFirebasePerformanceLogcat: "true"]
            resValue("string", "app_name", "Pera Wallet (Dev)")
        }
    }

    flavorDimensions "server"
    productFlavors {
        def apiKeyProps = new Properties()
        def apiUrlProps = new Properties()

        staging {
            dimension "server"
            versionNameSuffix " (STAGING)"
            applicationIdSuffix ".staging"
            buildConfigField "String", "APPLICATION_NAME", '"pera"'
            buildConfigField "String", "DEEPLINK_PREFIX", '"algorand://"'

            apiKeyProps.load(new FileInputStream(file('../app/src/staging/api-key.properties')))
            apiUrlProps.load(new FileInputStream(file('../app/src/main/api-url.properties')))

            buildConfigField "String", "ALGORAND_BASE_URL", apiUrlProps["NODE_TESTNET_URL"]
            buildConfigField "String", "ALGORAND_API_KEY", apiKeyProps["ALGORAND_API_KEY"]
            buildConfigField "String", "INDEXER_API_KEY", apiKeyProps["INDEXER_API_KEY"]
            buildConfigField "String", "MOBILE_API_KEY", apiKeyProps["MOBILE_API_KEY"]
            buildConfigField "String", "MOBILE_ALGORAND_MAINNET_BASE_URL", '"https://mainnet.staging.api.perawallet.app/"'
            buildConfigField "String", "MOBILE_ALGORAND_TESTNET_BASE_URL", '"https://testnet.staging.api.perawallet.app/"'
            buildConfigField "String", "PERA_3D_EXPLORER_BASE_URL", '"https://static-hosting.perawallet.app/3dviewer/index.html"'
            buildConfigField "String", "DISCOVER_WEBVIEW_USERNAME", apiKeyProps["DISCOVER_WEBVIEW_USERNAME"]
            buildConfigField "String", "DISCOVER_WEBVIEW_PASSWORD", apiKeyProps["DISCOVER_WEBVIEW_PASSWORD"]
            buildConfigField "String", "DISCOVER_VERSION", "\"${discoverVersions.staging}\""
            buildConfigField "String", "DISCOVER_URL", '"https://discover-mobile-staging.perawallet.app/"'
            buildConfigField "String", "DISCOVER_BROWSE_DAPP_URL", '"https://discover-mobile-staging.perawallet.app/main/browser"'
            buildConfigField "String", "MELD_URL", '"https://mainnet.staging.api.perawallet.app/v1/onramp-services/meld/redirect-to-fluidmoney/?walletAddress="'
            buildConfigField "String", "NODE_MAINNET_URL", apiUrlProps["NODE_MAINNET_URL"]
            buildConfigField "String", "NODE_TESTNET_URL", apiUrlProps["NODE_TESTNET_URL"]
            buildConfigField "String", "INDEXER_MAINNET_URL", apiUrlProps["INDEXER_MAINNET_URL"]
            buildConfigField "String", "INDEXER_TESTNET_URL", apiUrlProps["INDEXER_TESTNET_URL"]
        }

        prod {
            dimension "server"
            buildConfigField "String", "APPLICATION_NAME", '"pera"'
            buildConfigField "String", "DEEPLINK_PREFIX", '"algorand://"'

            apiKeyProps.load(new FileInputStream(file('../app/src/prod/api-key.properties')))
            apiUrlProps.load(new FileInputStream(file('../app/src/main/api-url.properties')))

            buildConfigField "String", "ALGORAND_BASE_URL", apiUrlProps["NODE_MAINNET_URL"]
            buildConfigField "String", "ALGORAND_API_KEY", apiKeyProps["ALGORAND_API_KEY"]
            buildConfigField "String", "INDEXER_API_KEY", apiKeyProps["INDEXER_API_KEY"]
            buildConfigField "String", "MOBILE_API_KEY", apiKeyProps["MOBILE_API_KEY"]
            buildConfigField "String", "MOBILE_ALGORAND_MAINNET_BASE_URL", '"https://mainnet.api.perawallet.app/"'
            buildConfigField "String", "MOBILE_ALGORAND_TESTNET_BASE_URL", '"https://testnet.api.perawallet.app/"'
            buildConfigField "String", "PERA_3D_EXPLORER_BASE_URL", '"https://static-hosting.perawallet.app/3dviewer/index.html"'
            buildConfigField "String", "DISCOVER_WEBVIEW_USERNAME", apiKeyProps["DISCOVER_WEBVIEW_USERNAME"]
            buildConfigField "String", "DISCOVER_WEBVIEW_PASSWORD", apiKeyProps["DISCOVER_WEBVIEW_PASSWORD"]
            buildConfigField "String", "DISCOVER_VERSION", "\"${discoverVersions.prod}\""
            buildConfigField "String", "DISCOVER_URL", '"https://discover-mobile.perawallet.app/"'
            buildConfigField "String", "DISCOVER_BROWSE_DAPP_URL", '"https://discover-mobile.perawallet.app/main/browser"'
            buildConfigField "String", "MELD_URL", '"https://mainnet.api.perawallet.app/v1/onramp-services/meld/redirect-to-fluidmoney/?walletAddress="'
            buildConfigField "String", "NODE_MAINNET_URL", apiUrlProps["NODE_MAINNET_URL"]
            buildConfigField "String", "NODE_TESTNET_URL", apiUrlProps["NODE_TESTNET_URL"]
            buildConfigField "String", "INDEXER_MAINNET_URL", apiUrlProps["INDEXER_MAINNET_URL"]
            buildConfigField "String", "INDEXER_TESTNET_URL", apiUrlProps["INDEXER_TESTNET_URL"]
        }
    }

    kapt {
        correctErrorTypes = true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        targetCompatibility = JavaVersion.VERSION_17
        sourceCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    buildFeatures {
        viewBinding true
        buildConfig = true
    }
    namespace application.applicationId
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation project(path: ':account')
    implementation project(path: ':nameservice-component')
    implementation project(path: ':transaction-history-component')
    implementation project(path: ':account-core-component')
    implementation project(path: ':assetdetail-component')
    implementation project(path: ':accountinfo-component')
    implementation project(path: ':custominfo-component')
    implementation project(path: ':parity')
    implementation project(path: ':asb-component')
    implementation project(path: ':accountcore-ui')
    implementation project(path: ':foundation')
    implementation project(path: ':deviceid-component')
    implementation project(path: ':contacts-component')
    implementation project(path: ':node-component')
    implementation project(path: ':assetsorting-component')
    implementation project(path: ':accountsorting-component')
    implementation project(path: ':drawable-ui')
    implementation project(path: ':designsystem')
    implementation project(path: ':transaction-component')
    implementation project(path: ':transaction-ui')
    implementation project(path: ':currency')
    implementation project(path: ':network-utils')
    implementation project(path: ':ledger-component')
    implementation project(path: ':banner-component')
    implementation project(path: ':swap-component')
    implementation project(path: ':swap-ui')
    implementation project(path: ':asset-utils')
    implementation project(path: ':date-ui')
    implementation project(path: ':transactionhistory-ui')
    implementation project(path: ':assetdetail-ui')
    implementation project(path: ':caching')
    implementation project(path: ':block-component')
    implementation project(path: ':app-cache-component')
    implementation project(path: ':notification-component')
    implementation project(path: ':pushtoken-component')
    implementation project(path: ':deeplink-component')
    implementation project(path: ':assetaction-ui')

    implementation deps.kotlinx.coroutines_core
    implementation deps.kotlinx.coroutines_android

    implementation deps.support.material
    implementation deps.support.flexbox
    implementation deps.support.appcompat
    implementation deps.support.constraintlayout

    implementation deps.android.navigation_fragment
    implementation deps.android.navigation_ui
    implementation deps.android.legacy
    implementation deps.android.core
    implementation deps.android.fragment_ktx
    implementation deps.android.multidex
    implementation deps.android.browser
    implementation deps.android.paging

    implementation deps.lifecycle.extensions
    implementation deps.lifecycle.viewmodel
    implementation deps.lifecycle.common

    implementation platform(deps.firebase.platform)
    implementation deps.firebase.analytics
    implementation deps.firebase.messaging
    implementation deps.firebase.crashlytics
    implementation deps.firebase.perf

    implementation deps.retrofit.core
    implementation deps.retrofit.converter_gson
    implementation deps.retrofit.logging_interceptor

    implementation deps.room.runtime
    implementation deps.room.ktx
    kapt deps.room.compiler

    implementation deps.hilt.core
    kapt deps.hilt.android_compiler
    kapt deps.hilt.compiler

    implementation deps.glide.core
    kapt deps.glide.compiler

    implementation deps.moshi.core
    implementation deps.moshi.kotlin

    // Wallet Connect v1
    implementation deps.wallet_connect_v1.web_socket
    implementation deps.wallet_connect_v1.khex

    // Wallet Connect v2
    implementation(platform(deps.wallet_connect_v2.platform))
    implementation(deps.wallet_connect_v2.core) {
        exclude group: 'com.jakewharton.timber', module: 'timber'
    }
    implementation deps.wallet_connect_v2.sign

    implementation deps.biometric
    implementation deps.zxing
    implementation deps.ble
    implementation deps.tink
    implementation deps.review
    implementation deps.lottie
    implementation deps.hipoexceptions
    implementation deps.charts
    implementation deps.exoplayer
    coreLibraryDesugaring deps.desugar_libs

    implementation(deps.algosdk) {
        exclude group: 'org.bouncycastle'
    }

    // Test
    testImplementation deps.junit
    androidTestImplementation deps.room_testing
    androidTestImplementation deps.runner
    androidTestImplementation deps.espresso
}
